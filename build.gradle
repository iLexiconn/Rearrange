buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "com.matthewprenger:CurseGradle:1.0-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

def secretFile = file "secret.properties"
project.ext.secret = null
if (secretFile.exists()) {
    secretFile.withReader {
        def prop = new Properties()
        prop.load(it)
        project.ext.secret = new ConfigSlurper().parse prop
    }
}

version = getVersionFromJava()
group = "net.ilexiconn"
archivesBaseName = config.mod_name
sourceCompatibility = targetCompatibility = 1.6

minecraft {
    version = config.minecraft_version + "-" + config.forge_version + "-" + config.minecraft_version
    runDir = "minecraft"
    mappings = config.forge_mappings
    replace "${version}", project.version
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven {
        name = "ilexiconn"
        url = "http://maven.ilexiconn.net/"
    }
}

dependencies {
    compile "net.ilexiconn:llibrary:${config.llibrary_version}-${config.minecraft_version}:dev"
}

processResources {
    inputs.property "version", getVersionFromJava()
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version
        expand "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task apiJar(type: Jar) {
    from (sourceSets.main.allSource) {
        include "net/ilexiconn/" + config.mod_name.toLowerCase() + "/api/**"
    }
    classifier = "api"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives apiJar
    archives javadocJar
}

import groovy.json.JsonSlurper
curseforge {
    def url = config.mod_versions.toURL()
    def versions = new JsonSlurper().parseText url.text

    if (secret) {
        apiKey = secret.curseForgeApiKey
    } else {
        apiKey = ""
    }

    project {
        id = config.mod_id

        releaseType = "release"
        changelog = versions.versions[versions.getAt("release")].join("\n")

        addGameVersion config.minecraft_version

        mainArtifact(jar) {
            displayName = config.mod_name + " " + getVersionFromJavaWithoutMc() + " for Minecraft " + config.minecraft_version
            relations {
                requiredLibrary "LLibrary"
            }
        }

        addArtifact(apiJar) {
            displayName = "API"
        }

        addArtifact(javadocJar) {
            displayName = "Javadoc"
        }
    }
}

String getVersionFromJava() {
    return getVersionFromJavaWithoutMc() + "-" + config.minecraft_version;
}

String getVersionFromJavaWithoutMc() {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String VERSION = \"";
    File file = file("src/main/java/net/ilexiconn/" + config.mod_name.toLowerCase() + "/" + config.mod_name + ".java");
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }

    return "$major.$revision.$patch";
}